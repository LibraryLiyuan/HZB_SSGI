// RayTracingCommon.ush
#pragma once
#include "/Engine/Private/Common.ush"

struct FHiZTraceInput
{
	// 屏幕空间坐标系下的光线起点
	float3 RayOrigin;
	float3 RayDirection;
	Texture2D HZBTexture;
	float4 HZBSize; 
	int MaxMipLevel;
	int MaxIterations;
	// 用于判断是否穿透到物体后面
	float Thickness;
	float2 ValidUVMin;
	float2 ValidUVMax;
};

struct FHiZTraceResult
{
	bool bHit;
	float3 HitUVz;
	float Iterations;
};

float2 GetCellCount(int MipLevel, float4 RootSize)
{
	return floor(RootSize.xy * exp2(-MipLevel));
}

FHiZTraceResult HiZTrace(FHiZTraceInput Input)
{
	FHiZTraceResult Result;
	Result.bHit = false;
	Result.HitUVz = float3(0, 0, 0);
	Result.Iterations = 0;

	float3 CurrentPos = Input.RayOrigin;
	float3 RayDir = Input.RayDirection;
	// AABB相交测试
	float3 InvRayDir;
	InvRayDir.x = (abs(RayDir.x) < 1e-6) ? 1e6 : 1.0 / RayDir.x;
	InvRayDir.y = (abs(RayDir.y) < 1e-6) ? 1e6 : 1.0 / RayDir.y;
	InvRayDir.z = (abs(RayDir.z) < 1e-6) ? 1e6 : 1.0 / RayDir.z;

	int CurrentMip = 0;
	float Iterations = 0;

	while (CurrentMip >= 0 && CurrentMip <= Input.MaxMipLevel && Iterations < Input.MaxIterations)
	{
		Iterations++;

		float2 CellCount = GetCellCount(CurrentMip, Input.HZBSize);
		float2 CellSize = 1.0 / CellCount;
		float2 CellUV = (floor(CurrentPos.xy * CellCount) + 0.5) * CellSize;
		// 当前HZB深度
		float HZB_DeviceZ = Input.HZBTexture.SampleLevel(GlobalPointClampedSampler, CellUV, CurrentMip).r;
		// 深度偏差，防止 Z-Fighting
		const float DepthEpsilon = 0.0001;

		if (CurrentPos.z < HZB_DeviceZ + DepthEpsilon) 
		{
			if (CurrentMip == 0)
			{
				// 将 DeviceZ (非线性) 转为 LinearDepth (线性，单位：厘米)
				// ConvertFromDeviceZ 是 UE Common.ush 里的函数
				float Linear_HZB = ConvertFromDeviceZ(HZB_DeviceZ);
				float Linear_Ray = ConvertFromDeviceZ(CurrentPos.z);
				
				float DistDiff = Linear_Ray - Linear_HZB;

				// 注意需要判断是否穿透了
				if (DistDiff > 0.0 && DistDiff < Input.Thickness)
				{
					Result.bHit = true;
					Result.HitUVz = float3(CurrentPos.xy, HZB_DeviceZ); 
					Result.Iterations = Iterations;
					return Result;
				}
				else
				{
					Result.bHit = false;
				}
			}
			else
			{
				// 重新开始循环，在更细的层级检查
				CurrentMip--;
				continue;
			}
		}
		
		{
			float2 CurrentCellIdx = floor(CurrentPos.xy * CellCount);
			// 计算光线的步进距离
			float2 BoundaryUV = (CurrentCellIdx + float2(RayDir.x > 0 ? 1.0 : 0.0, RayDir.y > 0 ? 1.0 : 0.0)) * CellSize;
			float2 T_Boundary = (BoundaryUV - CurrentPos.xy) * InvRayDir.xy;
			float StepT = min(T_Boundary.x, T_Boundary.y) + 0.0001;

			// 更新光线位置
			CurrentPos += RayDir * StepT;
			CurrentMip = min(CurrentMip + 1, Input.MaxMipLevel);
		}

		if (any(CurrentPos.xy < Input.ValidUVMin) || any(CurrentPos.xy > Input.ValidUVMax) || CurrentPos.z < 0.0001)
		{
			break;
		}
	}

	Result.Iterations = Iterations;
	return Result;
}